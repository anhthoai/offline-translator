import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def keystoreFile = Os.isFamily(Os.FAMILY_WINDOWS) ? "C:\\Dropbox\\documents\\keystore\\keystore1" : "/home/theis/keystore/keystore1"
def propertiesFile = Os.isFamily(Os.FAMILY_WINDOWS) ? "C:\\Dropbox\\documents\\keystore\\secure.properties" : "/home/theis/keystore/secure.properties"

Properties properties = new Properties()
properties.load(project.rootProject.file(propertiesFile).newDataInputStream())

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.rmtheis.translator.standalone"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 10
        versionName "1.2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(keystoreFile)
            storePassword = properties.getProperty("key.store.password.differentlyEscaped")
            keyAlias = "key1"
            keyPassword = properties.getProperty("key.alias.password.differentlyEscaped")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        flavor_catalan_occitan {
            applicationIdSuffix ".catalan.occitan"
            signingConfig signingConfigs.release
        }
        flavor_english_kurmanji {
            applicationIdSuffix ".english.kurmanji"
            signingConfig signingConfigs.release
        }
        flavor_english_sorani {
            applicationIdSuffix ".english.sorani"
            signingConfig signingConfigs.release
        }
        flavor_spanish_aragonese {
            applicationIdSuffix ".spanish.aragonese"
            signingConfig signingConfigs.release
        }
        flavor_spanish_asturian {
            applicationIdSuffix ".spanish.asturian"
            signingConfig signingConfigs.release
        }
        flavor_spanish_occitan {
            applicationIdSuffix ".spanish.occitan"
            signingConfig signingConfigs.release
        }
    }

    configurations.all {
        // Work around the following error for licensesdialog dependency:
        //     Error: Conflict with dependency 'com.google.code.findbugs:jsr305' in project ':app'.
        //     Resolved versions for app (3.0.1) and test app (2.0.1) differ.
        //     See http://g.co/androidstudio/app-test-app-conflict for details.
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.billingclient:billing:dp-1'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.google.android.gms:play-services-ads:11.4.0'
    compile('de.psdev.licensesdialog:licensesdialog:1.8.2')
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile project(':lttoolbox-java')
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}
